apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.diegomalone.githubviewer"
        minSdkVersion rootProject.mindkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        def STRING = "String"
        def API_URL = "API_URL"

        release {
            buildConfigField STRING, API_URL, "\"https://api.github.com/\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            buildConfigField STRING, API_URL, "\"https://api.github.com/\""
        }
    }

    packagingOptions {
        exclude('META-INF/rxjava.properties')
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'

        test {
            java.srcDir sharedTestDir
        }

        androidTest {
            java.srcDir sharedTestDir
        }

        main {
            file('src/main/res-screen')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }
}

kapt {
    generateStubs = true
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Support Library
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }

    // Test
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "com.github.fabioCollini.daggermock:daggermock:$daggerMockVersion"
    testImplementation "com.github.fabioCollini.daggermock:daggermock-kotlin:$daggerMockVersion"

    // Interface Test
    androidTestImplementation "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$expressoVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$expressoVersion") {
        exclude module: 'javax.annotation-api'
        exclude module: 'support-annotations'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$expressoVersion") {
        exclude module: 'espresso-core'
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
        exclude module: 'design'
    }

    androidTestImplementation("com.android.support.test:rules:$supportTestRulesVersion") {
        exclude module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test:runner:$supportTestRunnerVersion"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okHttpMockVersion"
}
